import java.time.LocalDateTime
import java.time.ZoneId
import java.time.ZoneOffset

buildscript {
  repositories {
    mavenCentral()
    google()
  }

  ext.kotlin_version = "1.5.30"

  dependencies {
    classpath 'com.android.tools.build:gradle:7.0.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.4"
    classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:5.3.0"
    classpath "com.nabilhachicha:android-native-dependencies:0.1.2"
    classpath 'com.google.gms:google-services:4.3.4'
    classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.2'
    classpath "de.mannodermaus.gradle.plugins:android-junit5:1.7.1.1"
  }
}

plugins {
  id "com.github.ben-manes.versions" version "0.39.0"
  id "ru.vyarus.animalsniffer" version "1.5.0"
}

ext {
  android_build_tools_version = "30.0.2"
  android_compile_sdk_version = 29
  android_min_sdk_version = 21
  android_target_sdk_version = 29

  // Required for some dependencies only available from our private S3
  //
  nyplS3Depend =
    project.findProperty('org.librarysimplified.s3.depend') as Boolean
  nyplS3AccessKey =
    project.findProperty('org.librarysimplified.s3.access_key_id') ?: ''
  nyplS3SecretKey =
    project.findProperty('org.librarysimplified.s3.secret_access_key') ?: ''

  if (nyplS3Depend && !nyplS3AccessKey) {
    throw new GradleException(
      """\
      'org.librarysimplified.s3.access_key_id' is undefined!
        ^ required by 'org.librarysimplified.s3.access_key_id'\
      """.stripIndent())
  }
  if (nyplS3Depend && !nyplS3SecretKey) {
    throw new GradleException(
      """\
      'org.librarysimplified.s3.secret_access_key' is undefined!
        ^ required by 'org.librarysimplified.s3.depend'\
      """.stripIndent())
  }

  // Required by some projects to build with drm support
  //
  nyplDrmEnabled =
    project.findProperty('org.librarysimplified.drm.enabled') as Boolean
  if (nyplDrmEnabled && !nyplS3Depend) {
    throw new GradleException(
      """\
      'org.librarysimplified.s3.depend' is undefined!
        ^ required by 'org.librarysimplified.drm.enabled'\
      """.stripIndent())
  }

  //
  // Check that non -SNAPSHOT builds don't use -SNAPSHOT dependencies.
  //

  final String versionName = project.ext["VERSION_NAME"]
  final Set<String> snapshots = new HashSet<>()

  if (!versionName.endsWith("-SNAPSHOT")) {
    final List<String> versionAliases = libs.getVersionAliases()
    versionAliases.each { name ->
      final java.util.Optional<VersionConstraint> versionConstraintOpt = libs.findVersion(name)
      if (versionConstraintOpt.isPresent()) {
        final VersionConstraint versionConstraint = versionConstraintOpt.get()
        if (versionConstraint.requiredVersion.endsWith("-SNAPSHOT")) {
          snapshots.add("$name ${versionConstraint.requiredVersion}")
        }
      }
    }

    if (!snapshots.isEmpty()) {
      final StringBuilder builder = new StringBuilder();
      builder.append("The current non-SNAPSHOT build (")
      builder.append(versionName)
      builder.append(") depends on the following -SNAPSHOT dependencies:")
      builder.append("\n")
      snapshots.each { version ->
        builder.append("  ")
        builder.append(version)
        builder.append("\n")
      }
      final String text = builder.toString()
      logger.error("{}", text)
      throw new GradleException(text);
    }
  }
}

subprojects { project ->
  group = project.ext["GROUP"]
  version = project.ext["VERSION_NAME"]

  switch (POM_PACKAGING) {
    case "aar":
      apply from: file("$rootDir/build_aar.gradle")
      break
    case "apk":
      apply from: file("$rootDir/build_apk.gradle")
      break
    case "jar":
      apply from: file("$rootDir/build_jar.gradle")
      break
    default:
      throw new IllegalStateException(
        "Unrecognized packaging type ${POM_PACKAGING} for ${project}")
  }

  /*
   * Configure publishing for the various project types.
   */

  switch (POM_PACKAGING) {
    case "jar":
      apply from: file("$rootDir/build_publishing.gradle")
      break
    case "apk":
      break
    case "aar":
      apply from: file("$rootDir/build_publishing.gradle")
      break
    default:
      throw new IllegalStateException(
        "Unrecognized packaging type ${POM_PACKAGING} for ${project}")
  }

  repositories {
    mavenLocal()
    mavenCentral()
    google()

    /*
     * S3 repositories for restricted DRM artifacts.
     */

    if (nyplDrmEnabled) {
      maven {
        name = "S3 Snapshots"
        url = "s3://simplye-maven-builds/snapshots/"
        credentials(AwsCredentials) {
          accessKey = nyplS3AccessKey
          secretKey = nyplS3SecretKey
        }
        mavenContent {
          snapshotsOnly()
        }
      }

      maven {
        name = "S3 Releases"
        url = "s3://simplye-maven-builds/releases/"
        credentials(AwsCredentials) {
          accessKey = nyplS3AccessKey
          secretKey = nyplS3SecretKey
        }
        mavenContent {
          releasesOnly()
        }
      }
    }

    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven {
      url "http://maven.findawayworld.com/artifactory/libs-release/"
      allowInsecureProtocol true
    }
    maven {
      url "http://maven.findawayworld.com/artifactory/libs-snapshot/"
      allowInsecureProtocol true
    }
    maven { url "https://jitpack.io" }
    jcenter()
  }

  // Skip tasks for projects that require drm; unless drm is enabled
  //
  def nyplDrmRequired =
    project.findProperty('org.librarysimplified.drm.required') as Boolean
  if (nyplDrmRequired) {
    project.tasks.all { task ->
      task.onlyIf { nyplDrmEnabled }
    }
  }
}

/**
 * An empty task called by CI. We don't do semantic versioning analysis here yet.
 */
task verifySemanticVersioning {}

/**
 * Displays properties starting with 'org.librarysimplified'.
 */

task listProperties(group: 'NYPL') {
  description = "Displays properties starting with 'org.librarysimplified'."
  doLast {
    project.properties
      .findAll { p ->
        p.key.startsWith('org.librarysimplified')
      }
      .sort()
      .each { println(it) }
  }
}

/*
 * Add ktlint tasks.
 */

apply from: file("$rootDir/build_ktlint.gradle")

subprojects {
  assemble.dependsOn ktlint
}

/**
 * Expand the 'tilde' character to user.home.
 */
@SuppressWarnings('unused')
static String expandUserHome(String path) {
  if (path == null) return
  return path.replaceFirst("~", System.getProperty("user.home"))
}

@SuppressWarnings('unused')
static Integer calculateVersionCode(Project project) {
  def now = LocalDateTime.now(ZoneId.of("UTC"))
  def nowSeconds = now.toEpochSecond(ZoneOffset.UTC)
  // Seconds since 2021-03-15 09:20:00 UTC
  def nowReduced = nowSeconds - 1615800000
  return nowReduced
}

task listrepos {
    doLast {
        println "Repositories:"
        project.repositories.each { println "Name: " + it.name + "; url: " + it.url }
   }
}
