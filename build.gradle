buildscript {
  ext.kotlin_version = '1.2.50'
  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.1.4'
    classpath 'com.netflix.nebula:gradle-lint-plugin:7.4.0'
    classpath 'com.nabilhachicha:android-native-dependencies:0.1.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

ext {

  /*
   * The version of the NYPL Audiobook API used by this package.
   */

  nypl_audiobook_api_version = "0.0.64"
}

allprojects {
  group = 'org.nypl.simplified'
  version = '2.1.1'

  apply plugin: 'maven'
  apply plugin: 'nebula.lint'

  gradleLint.alwaysRun = false
  gradleLint.rules = ['all-dependency']
}

subprojects {
  repositories {
    mavenLocal()
    maven {
      credentials {
        username findProperty("org.librarysimplified.nexus.username")
        password findProperty("org.librarysimplified.nexus.password")
      }
      url "https://nexus.librarysimplified.org:8443/nexus/content/groups/external/"
    }
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "https://jitpack.io" }
    // Needed for support-v4 26.0.0+
    maven { url "https://maven.google.com" }

    jcenter()

    if (project.hasProperty("org.nypl.simplified.with_findaway")) {
      if (project.property("org.nypl.simplified.with_findaway") == "true") {
        maven {
          url "http://maven.findawayworld.com/artifactory/libs-release/"
        }
      }
    }
  }
}
