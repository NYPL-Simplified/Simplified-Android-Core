import org.librarysimplified.gradle.RequiredAssetsTask

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// Fail the build if these assets aren't present
//
def requiredFiles = [:]
requiredFiles["$projectDir/src/main/assets/ReaderClientCert.sig"] =
  "280838a4cb5c2ca4a2c8cf14e93f8ff72dbc6046cf188b5e83cf830f5159ce1e"
requiredFiles["$projectDir/src/main/assets/secrets.conf"] =
  "dde8800740b5c776df043b48a66be2952d4c6954d58f31ec42afcebba66383ca"

android {
  defaultConfig {
    versionName = project.version
    versionCode = versionCode(project.version)
    setProperty("archivesBaseName", "simplye-${versionName}")
  }
  applicationVariants.all { variant ->
    def finalizerTask =
      tasks.create(name: "requiredFiles${variant.name.capitalize()}", type: RequiredAssetsTask) {
        required = requiredFiles
        apkFiles = variant.outputs.collect { out ->
          out.outputFile
        }
      }
    variant.assembleProvider.configure {
      finalizedBy finalizerTask
    }
  }
}

dependencies {
  implementation project(":simplified-main")
  implementation project(":simplified-accounts-source-nyplregistry")
  implementation project(":simplified-analytics-circulation")
  implementation project(":simplified-crashlytics")
  implementation project(":simplified-migration-from3master")
  implementation libraries.firebase_analytics
  implementation libraries.firebase_crashlytics
  implementation libraries.nypl_drm_core
  implementation libraries.nypl_findaway
  implementation libraries.nypl_overdrive

  if (nyplNexusDepend) {
    implementation libraries.nypl_drm_adobe
  }
  annotationProcessor libraries.google_autovalue_processor
}
