import org.librarysimplified.gradle.RequiredAssetsTask

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-kapt'

// Fail the build if these assets aren't present
//
def requiredFiles = [:]
requiredFiles["ReaderClientCert.sig"] =
  "280838a4cb5c2ca4a2c8cf14e93f8ff72dbc6046cf188b5e83cf830f5159ce1e"
requiredFiles["secrets.conf"] =
  "5e459128eaf8d425f381aa09e9e6081d183cbd4b217b9ba528cdb2d57b63aa87"

android {
  buildFeatures {
    dataBinding true
  }

  defaultConfig {
    versionName = project.version
    versionCode = calculateVersionCode(project)
    setProperty("archivesBaseName", "simplye")
  }

  applicationVariants.all { variant ->
    def finalizerTask =
      tasks.create(name: "requiredFiles${variant.name.capitalize()}", type: RequiredAssetsTask) {
        required = requiredFiles
        apkFiles = variant.outputs.collect { out ->
          out.outputFile
        }
      }
    variant.assembleProvider.configure {
      finalizedBy finalizerTask
    }
  }
}

dependencies {
  implementation project(":simplified-main")
  implementation project(":simplified-accounts-source-nyplregistry")
  implementation project(":simplified-analytics-circulation")
  implementation project(":simplified-crashlytics")
  implementation libs.firebase.analytics
  implementation libs.firebase.crashlytics
  implementation libs.nypl.readium
  implementation libs.nypl.drm.core
  implementation libs.nypl.findaway
  implementation libs.nypl.overdrive
  implementation libs.nypl.theme

  if (nyplS3Depend) {
    implementation libs.nypl.drm.adobe
  }
  annotationProcessor libs.google.auto.value.processor
}
