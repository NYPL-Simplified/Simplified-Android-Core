apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def versionValues() {
  def propsFile = file('version.properties')
  def Properties props = new Properties()
  def name
  def code
  if (propsFile.canRead()) {
    props.load(new FileInputStream(propsFile))
    name = props['versionName']
    code = props['versionCode'].toInteger()
  } else {
    throw new FileNotFoundException("Could not read version.properties!")
  }

  props['versionCode'] = (code + 1).toString()
  props.save(new FileOutputStream(propsFile), "")

  logger.info("incrementing build version ${code} -> ${code + 1}")
  return new Tuple(name, code)
}

android {
  compileSdkVersion 27
  buildToolsVersion "28.0.2"

  def version = versionValues()

  packagingOptions {
    exclude 'META-INF/LICENSE'
  }

  signingConfigs {
    release {
      keyAlias findProperty("org.librarysimplified.simplye.keyAlias")
      keyPassword findProperty("org.librarysimplified.simplye.keyPassword")
      storeFile file("keystore.jks")
      storePassword findProperty("org.librarysimplified.simplye.storePassword")
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
    }
  }

  defaultConfig {
    multiDexEnabled true
    versionName = version.get(0)
    versionCode = version.get(1)
    setProperty("archivesBaseName", "simplye-${versionName}-${versionCode}")
    minSdkVersion androidMinimumSDK
  }

  lintOptions {
    checkReleaseBuilds false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

description = 'simplified-app-simplye'

dependencies {
  implementation project(':simplified-app-shared')
  implementation project(':simplified-analytics-circulation')

  implementation group: 'org.nypl.drm', name: 'nypl-drm-adobe-provider', version: '1.0.0'
  implementation group: 'org.nypl.drm', name: 'libnypl_adobe', version: '1.0.0'
  implementation group: 'org.nypl.drm', name: 'libnypl_adobe_filter', version: '1.0.0'

  implementation "com.android.support:support-v4:27.1.1"
}

apply plugin: 'android-native-dependencies'

native_dependencies {
  artifact (group: 'org.nypl.drm', name: 'libnypl_adobe', version: '1.0.0',
      classifier: 'armeabi-v7a') {
    addLibPrefixToArtifact=false
  }

  artifact (group: 'org.nypl.drm', name: 'libnypl_adobe_filter', version: '1.0.0',
      classifier: 'armeabi-v7a') {
    addLibPrefixToArtifact=false
  }
}
