def versionValues() {
  def props = new Properties()
  file("version.properties").withInputStream { props.load(it) }

  // Load properties
  def versionName = props.getProperty("versionName")
  def versionCode = props.getProperty("versionCode").toInteger()

  // Increment version using the build number, if on Travis CI
  def buildNumber = (System.getenv('TRAVIS_BUILD_NUMBER') ?: "0").toInteger()
  if (buildNumber > 0) {
    logger.info("Incrementing version code: $versionCode + $buildNumber = ${versionCode + buildNumber}")
    versionCode += buildNumber
  }
  return new Tuple(versionName, versionCode)
}

android {
  def version = versionValues()

  defaultConfig {
    versionName = version.get(0)
    versionCode = version.get(1)
    setProperty("archivesBaseName", "${versionName}-${versionCode}")
  }

  packagingOptions {
    doNotStrip 'lib/**/*.so'

    // Readium and the PDF reader both provide this shared library. This causes
    // the build to fail with an error because Gradle doesn't know which to pick.
    pickFirst 'lib/x86/libc++_shared.so'
    pickFirst 'lib/arm64-v8a/libc++_shared.so'
    pickFirst 'lib/armeabi-v7a/libc++_shared.so'
  }

  signingConfigs {
    release {
      keyAlias findProperty("org.librarysimplified.keyAlias")
      keyPassword findProperty("org.librarysimplified.keyPassword")
      storeFile file("keystore.jks")
      storePassword findProperty("org.librarysimplified.storePassword")
    }
  }

  buildTypes {
    debug {
      ndk { abiFilters 'x86', 'arm64-v8a', 'armeabi-v7a' }
      versionNameSuffix '-debug'
    }
    release {
      ndk { abiFilters 'arm64-v8a', 'armeabi-v7a' }
      signingConfig signingConfigs.release
    }
  }

  flavorDimensions 'mode'

  productFlavors {
    vanilla {
      dimension 'mode'
    }
    vanillaWithProfiles {
      dimension 'mode'
      applicationIdSuffix ".with_profiles"
    }
  }
}

dependencies {
  implementation project(":simplified-main")
  implementation project(":simplified-accounts-source-nyplregistry")
  annotationProcessor libraries.autoValueProcessor

  vanillaWithProfilesImplementation project(":simplified-accounts-source-filebased")
}
